plugins {
    id "java"
    id "groovy"
    id "maven"
    id "jacoco"
    id "com.github.kt3k.coveralls" version "2.8.2"
}

ext.moduleName = 'photoshelf.storage'

group 'org.photoshelf'
version '0.0.1-SNAPSHOT'

sourceCompatibility = 9

compileJava {
    inputs.property("moduleName", moduleName)
    doFirst {
        options.compilerArgs = [
                '--module-path', classpath.asPath,
                '--add-modules', 'java.activation',
        ]
        classpath = files()
    }
}

compileTestJava {
    inputs.property("moduleName", moduleName)
    doFirst {
        options.compilerArgs = [
                '--module-path', classpath.asPath,
                '--add-modules', 'java.activation,junit',
                '--add-reads', "$moduleName=junit",
                '--patch-module', "$moduleName=" + files(sourceSets.test.java.srcDirs).asPath,
        ]
        classpath = files()
    }
}
test {
    inputs.property("moduleName", moduleName)
    doFirst {
        jvmArgs = [
                '--add-modules', 'java.activation',
        ]
    }
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

dependencies {
    // https://mvnrepository.com/artifact/org.apache.httpcomponents/httpclient
    implementation group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.3'
    // https://mvnrepository.com/artifact/org.apache.httpcomponents/httpmime
    implementation group: 'org.apache.httpcomponents', name: 'httpmime', version: '4.5.3'
    // https://mvnrepository.com/artifact/com.google.code.gson/gson
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.1'

    testCompile group: 'junit', name: 'junit', version: '4.12'
    // https://mvnrepository.com/artifact/com.github.tomakehurst/wiremock
    testCompile group: 'com.github.tomakehurst', name: 'wiremock', version: '2.7.1'

    testCompile 'org.spockframework.spock:spock-core:spock-1.1'
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "file://localhost/var/mvn/repo")
        }
    }
}
